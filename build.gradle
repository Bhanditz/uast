buildscript {
    ext.kotlin_version = '1.0.2'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

allprojects {
    group = 'org.jetbrains.uast'
    version = System.getenv("ARTIFACT_VERSION")

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: "maven-publish"

    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided

            java.srcDirs = ['src']
            kotlin.srcDirs = ['src']
        }
    }

    compileJava {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
    }

    repositories {
        jcenter()
    }

    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_API_KEY")

        publications = ['UastPublication']

        pkg {
            repo = 'uast'
            name = 'uast'
            userOrg = 'kotlin'
            licenses = ['Apache-2.0']
            vcsUrl = SCM_URL

            version {
                name = System.getenv("ARTIFACT_VERSION")
                released = new Date()
            }
        }
    }

    publishing {
        publications {
            UastPublication(MavenPublication) {
                from components.java
                groupId 'org.jetbrains.uast'
                artifactId project.name
                version System.getenv("ARTIFACT_VERSION")
            }
        }
    }
}